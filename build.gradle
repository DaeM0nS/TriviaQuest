buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
			url = 'https://oss.sonatype.org/content/groups/public' 
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '2.2.2'
group = "com.mcsimonflash.daem0ns.forge.triviaquest"
archivesBaseName = "TriviaQuest-Forge"

minecraft {
    version = "1.12.2-14.23.5.2838"
    mappings = 'stable_39'
	runDir = "run"
}

repositories {
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile(group: 'ninja.leaping.configurate', name: 'configurate-hocon', version: '3.7.1') {
        exclude module: 'guava'
    }
}

jar{
    includeEmptyDirs = false
    classifier = 'server'
    version = "${project.minecraft.version}-v${project.version}"
}

def repackPackage = 'repack'
shadowJar {
    classifier = 'universal' // This is now the main jar
    relocate 'com.typesafe.config', "${repackPackage}.com.typesafe.config"
    relocate 'ninja.leaping.configurate', "${repackPackage}.ninja.leaping.configurate"
}

[jar, shadowJar, sourceJar]*.version = "$minecraft.version-$project.version"

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}


// Enables use of the section character (ยง) instead of long tags. Thanks, Hiroku!
//noinspection GroovyAssignabilityCheck
tasks.withType(JavaCompile)
{
    task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}
compileJava.options.encoding = 'UTF-8'
